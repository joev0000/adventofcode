ECHO =================
ECHO Boot & Run PAL-11
ECHO =================

ECHO ...Set the CPU parameters
SET CPU 11/20
SET CPU 56K

ECHO ...disable undesired devices
SET DZ DISABLE
SET RK DISABLE
SET RL DISABLE
SET HK DISABLE
SET RX DISABLE
SET RP DISABLE
SET RQ DISABLE
SET TM DISABLE
SET TQ DISABLE

ECHO ...enable paper tape punch and line printer
SET PTP ENABLE
SET LPT ENABLE

ECHO ...Load Paper Tape bootstrap loader @ 28kW
DEPOSIT 157744 016701
DEPOSIT 157746 000026
DEPOSIT 157750 012702
DEPOSIT 157752 000352
DEPOSIT 157754 005211
DEPOSIT 157756 105711
DEPOSIT 157760 100376
DEPOSIT 157762 116162
DEPOSIT 157764 000002
DEPOSIT 157766 157400
DEPOSIT 157770 005267
DEPOSIT 157772 177756
DEPOSIT 157774 000765
DEPOSIT 157776 177550
DEPOSIT SR 157744

ECHO ...attach ABSOLUTE LOADER papertape
SET PTR ENABLE
ATTACH PTR DEC-11-L2PC-PO.ptap

ECHO ...Execute bootstrap to load Absolute Loader
GO 157744

ECHO ...Attach PAL-11A (8K) papertape
ATTACH PTR DEC-11-ASXA-PB.ptap

; Set up responses to PAL-11A initialization
EXPECT "*S " ATTACH PTR %1.pal11a; SEND AFTER=20000,"H\r\n"; CONT
EXPECT "*B " SEND AFTER=20000,"H/E\r\n"; CONT
EXPECT "*L " SEND AFTER=20000,"H\r\n"; CONT
EXPECT "*T " SEND AFTER=20000,"H/3\r\n"; CONT
EXPECT " END ?" ATTACH PTR %1.pal11a; ATTACH PTP -n %1.lda.ptap; SEND AFTER=20000,"\r\n"; CONT
EXPECT " END ?" ATTACH PTR %1.pal11a; ATTACH PTP -n %1.out; SEND AFTER=20000,"\r\n"; CONT
EXPECT "*S " EXIT

ECHO ...Use absolute loader to load and launch the content of the papertape
DEPOSIT SR 157500
; Absolute loader occupies xx7474 through xx7743
; start address is xx7500
GO 157500

